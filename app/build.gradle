apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: "androidx.navigation.safeargs.kotlin"

// For Epoxy
kapt {
    correctErrorTypes = true
}

android {
    kotlinOptions {
        jvmTarget = '1.6'
    }
    compileSdkVersion rootProject.ext.compileSdk
    defaultConfig {
        applicationId rootProject.ext.applicationId
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        dev {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    def build_param = "${build}"

    if (build_param != "dev") {
        //exclude production build
        android.variantFilter { variant ->
            if (variant.buildType.name == 'dev') {
                variant.setIgnore(true)
            }
        }
    } else {
        //exclude all except production build
        android.variantFilter { variant ->
            if (variant.buildType.name != 'dev') {
                variant.setIgnore(true)
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    androidExtensions {
        experimental = true
    }
    dynamicFeatures = [":about"]
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation Libs.org_jetbrains_kotlin_kotlin_stdlib_jdk7

    implementation project(path: ':core')
    implementation project(path: ':repository')
    implementation project(path: ':mvrxlite')

    implementation Libs.appcompat
    implementation Libs.constraintlayout
    implementation Libs.legacy_support_v4
    implementation Libs.lifecycle_extensions
    implementation Libs.material
    implementation Libs.core_ktx
    implementation Libs.preference
    implementation Libs.legacy_preference_v14

    implementation Libs.navigation_fragment_ktx
    implementation Libs.navigation_ui_ktx

    implementation Libs.koin_android
    implementation Libs.koin_androidx_scope
    implementation Libs.koin_androidx_viewmodel

    implementation Libs.rxjava
    implementation Libs.rxandroid
    implementation Libs.rxkotlin
    implementation Libs.rxbinding
    implementation Libs.rxrelay

    implementation Libs.glide
    kapt Libs.com_github_bumptech_glide_compiler

    implementation Libs.com_pierfrancescosoffritti_androidyoutubeplayer_core
    implementation Libs.lemniscate
    implementation Libs.epoxy
    kapt Libs.epoxy_processor

    testImplementation Libs.junit
    testImplementation Libs.mockito_core
    androidTestImplementation Libs.androidx_test_runner
    androidTestImplementation Libs.espresso_core

    implementation Libs.firebase_core
    implementation Libs.crashlytics
    implementation Libs.okhttp
}

apply plugin: 'com.google.gms.google-services'
